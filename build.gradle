import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'fabric-loom' version '1.11-SNAPSHOT'
	id 'maven-publish'
	id 'org.jetbrains.kotlin.jvm' version "2.1.0"
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

repositories {
	mavenCentral()
	mavenLocal()

	flatDir {
		dirs "libs"
	}

	maven {
		url = "https://maven.nucleoid.xyz"
	}
	maven {
		url = "https://oss.sonatype.org/content/repositories/snapshots"
	}
	maven {
		url = "https://jitpack.io"
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Yes this has to be capitalised
	modImplementation "com.github.quiltservertools:Ledger:1.3.10+local"

	modImplementation "net.fabricmc:fabric-language-kotlin:1.13.5+kotlin.2.2.10"

	implementation(include("com.uchuhimo:konf-core:1.1.2"))

	implementation(include("com.zaxxer:HikariCP:7.0.2"))

	// SQLite
	implementation(include("org.xerial:sqlite-jdbc:3.50.3.0"))

	// H2
	implementation(include("com.h2database:h2:2.3.232"))

	// MySQL
	implementation(include('com.mysql:mysql-connector-j:9.4.0'))

	// MariaDB
	implementation(include('org.mariadb.jdbc:mariadb-java-client:3.5.5'))

	// PostgreSQL
	implementation(include("org.postgresql:postgresql:42.7.7"))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 21
}

tasks.withType(KotlinCompile).configureEach {
	it.compilerOptions.jvmTarget = JvmTarget.JVM_21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
	}
}
